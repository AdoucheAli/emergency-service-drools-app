package ambulancerules;


import com.wordpress.salaboy.model.Patient;
import com.wordpress.salaboy.model.Emergency.EmergencyType;
import com.wordpress.salaboy.model.Doctor.DoctorSpeciality;
import com.wordpress.salaboy.model.Emergency;
import com.wordpress.salaboy.model.Ambulance;
import com.wordpress.salaboy.model.Doctor;
import com.wordpress.salaboy.model.MedicalKit;
import org.drools.runtime.process.WorkflowProcessInstance;
import java.util.Map;
import java.util.List;
import com.wordpress.salaboy.events.MapEmergencyReachedEventNotifier;
import com.wordpress.salaboy.events.MapEventsNotifier;
import com.wordpress.salaboy.events.MapEventsNotifier.EventType;


global Map ambulances;
global Map doctors;
global MapEventsNotifier mapEventsNotifier;

rule "Initialize Emergency"
    ruleflow-group "select_ambulance"
    no-loop true
    lock-on-active true
    salience 20
    when
      $processInstance: WorkflowProcessInstance()
      $emergency: Emergency(call.processId == $processInstance.id)
      
    then
     // Create the patient based on the information collected in the first Human Task
     Patient patient = new Patient((String)$processInstance.getVariable("patient.name"),
                                Integer.valueOf((String)$processInstance.getVariable("patient.age")),
                                (String)$processInstance.getVariable("patient.gender") );
     $emergency.setPatient(patient);
     $emergency.setType(EmergencyType.valueOf((String)$processInstance.getVariable("emergency.type")));
     
     
     // Sets the patient Id as a process variable
     $processInstance.setVariable("patient.id", patient.getId());
     
    //ADD NOTIFIER.. because a new Emergency with a call was created!
    //System.out.println("ADDING EMERGENCY REACHED LISTENER NOTIFIER");
    // mapEventsNotifier.addWorldEventNotifier(EventType.EMERGENCY_REACHED, new MapEmergencyReachedEventNotifier($emergency));
    mapEventsNotifier.addWorldEventNotifier(EventType.EMERGENCY_REACHED, $emergency);
     

     // Insert the emergency in our world
     update($emergency);
     //Insert the patient in our world
     insert(patient);
end

rule "Fire Emergency without more information"
    ruleflow-group "select_ambulance"
    salience 10
    no-loop true
    when
        $processInstance: WorkflowProcessInstance()
        $emergency: Emergency($type : type == EmergencyType.FIRE)
    then
    	// Get the first ambulance that we have for this type of emergency
        Ambulance ambulance = (Ambulance)((List) ambulances.get($type)).get(0);
        // Get the first doctor that we have with the speciality related with the emergency
        Doctor doctor = (Doctor)((List)doctors.get(DoctorSpeciality.BURNS)).get(0);
        ambulance.setDoctorOnBoard(doctor);
        $emergency.setAmbulance(ambulance);
        //Update Emergency Information
        update($emergency);
		// Set the doctor.id and the ambulance.id as process variables for future references
        $processInstance.setVariable("doctor.id", doctor.getId());
        $processInstance.setVariable("ambulance.id", ambulance.getId());
        $processInstance.setVariable("ambulance.description", ambulance.getDescription());
        
end

rule "Car Crash Emergency without more information"
    ruleflow-group "select_ambulance"
    salience 10
    no-loop true
    when
        $processInstance: WorkflowProcessInstance()
        $emergency: Emergency($type: type == EmergencyType.CAR_CRASH)
    then
    	// Get the first ambulance that we have for this type of emergency
        Ambulance ambulance = (Ambulance)((List) ambulances.get($type)).get(0);
        // Get the first doctor that we have with the speciality related with the emergency
        Doctor doctor = (Doctor)((List)doctors.get(DoctorSpeciality.BONES)).get(0);
        ambulance.setDoctorOnBoard(doctor);
        $emergency.setAmbulance(ambulance);
        //Update Emergency Information
        update($emergency);
		// Set the doctor.id and the ambulance.id as process variables for future references
        $processInstance.setVariable("doctor.id", doctor.getId());
        $processInstance.setVariable("ambulance.id", ambulance.getId());
        $processInstance.setVariable("ambulance.description", ambulance.getDescription());
        
end

rule "Heart Attack Emergency without more information"
    ruleflow-group "select_ambulance"
    salience 10
    no-loop true
    when
        $processInstance: WorkflowProcessInstance()
        $emergency: Emergency($type: type == EmergencyType.HEART_ATTACK)
    then
    	// Get the first ambulance that we have for this type of emergency
        Ambulance ambulance = (Ambulance)((List) ambulances.get($type)).get(0);
        // Get the first doctor that we have with the speciality related with the emergency
        Doctor doctor = (Doctor)((List)doctors.get(DoctorSpeciality.REANIMATION)).get(0);
        ambulance.setDoctorOnBoard(doctor);
        $emergency.setAmbulance(ambulance);
        //Update Emergency Information
        update($emergency);
		// Set the doctor.id and the ambulance.id as process variables for future references
        $processInstance.setVariable("doctor.id", doctor.getId());
        $processInstance.setVariable("ambulance.id", ambulance.getId());
        $processInstance.setVariable("ambulance.description", ambulance.getDescription());
        
end

rule "Car Crash Emergency with Patient older than 80 years old"
    ruleflow-group "select_ambulance"
    salience 5
    no-loop true
    when
        $processInstance: WorkflowProcessInstance()
        $emergency: Emergency($type: type == EmergencyType.CAR_CRASH)
        $patient: Patient(age > 80)
    then
    	// For this special situation we need to put in the ambulance a special stroller
        Ambulance ambulance = $emergency.getAmbulance();
        ambulance.addKit(new MedicalKit("special stroller", DoctorSpeciality.BONES));
        
end
