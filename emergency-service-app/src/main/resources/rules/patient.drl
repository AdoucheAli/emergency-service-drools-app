package patient.wiimote;

import java.util.ArrayList;
import com.wordpress.salaboy.events.PulseEvent;
import com.wordpress.salaboy.ui.MapEventsNotifier;
import com.wordpress.salaboy.EmergencyService;
import com.wordpress.salaboy.model.Emergency;


declare PulseEvent
    @role ( event )
    @expires ( 20s )
end

rule "Patient with no vital sign"
    when
        $emergency: Emergency()
        Number(doubleValue == 235 ) from accumulate( PulseEvent(processed == false, $pulse: value) over window:time(4s) from entry-point "patientHeartbeats",
        												average($pulse)) 
    then
        EmergencyService.getInstance().getMapEventsNotifier()
            .notifyMapEventsListeners(MapEventsNotifier.EventType.NO_VITAL_SIGNS, new MonitorAlertReceivedNotifierEvent("Warning, patient without vital signs ", $emergency.getId()));
end

rule "Patient heart attack"
    when
        $emergency: Emergency()
        ArrayList(size > 10 ) from collect( PulseEvent( processed == false, $pulse: value) over window:time(1s) from entry-point "patientHeartbeats") 
    then
        EmergencyService.getInstance().getMapEventsNotifier()
            .notifyMapEventsListeners(MapEventsNotifier.EventType.HEART_ATTACK, new MonitorAlertReceivedNotifierEvent("Warning, patient suffering a heart attack ", $emergency.getId()));
end