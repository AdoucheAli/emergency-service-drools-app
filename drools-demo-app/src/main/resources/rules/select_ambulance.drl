package ambulancerules;


import com.wordpress.salaboy.model.Patient;
import com.wordpress.salaboy.model.Emergency.EmergencyType;
import com.wordpress.salaboy.model.Medic.MedicSpeciality;
import com.wordpress.salaboy.model.Emergency;
import com.wordpress.salaboy.model.Ambulance;
import com.wordpress.salaboy.model.Medic;
import com.wordpress.salaboy.model.MedicalKit;
import org.drools.runtime.process.WorkflowProcessInstance;
import java.util.Map;
import java.util.List;
import com.wordpress.salaboy.ui.MapEventsNotifier;
import com.wordpress.salaboy.ui.MapEventsNotifier.EventType;
import com.wordpress.salaboy.events.MapEmergencyReachedEventNotifier;
import com.wordpress.salaboy.EmergencyService;
import com.wordpress.salaboy.graphicable.*;


global Map ambulances;
global Map doctors;


rule "Initialize Emergency"
    ruleflow-group "select_ambulance"
    no-loop true
    lock-on-active true
    salience 20
    when
      $processInstance: WorkflowProcessInstance()
      $emergency: Emergency(call.processId == $processInstance.id)
      
    then
     // Get the Emergency from the Process Variable
     //Emergency emergency = (Emergency)$processInstance.getVariable("emergency");
     
     // Create the patient based on the information collected in the first Human Task
     Patient patient = new Patient((String)$processInstance.getVariable("patient.name"),
                                Integer.valueOf((String)$processInstance.getVariable("patient.age")),
                                (String)$processInstance.getVariable("patient.gender") );
     $emergency.setPatient(patient);
     $emergency.setType(EmergencyType.valueOf((String)$processInstance.getVariable("emergency.type")));
     
     
     // Sets the patient Id as a process variable
     $processInstance.setVariable("patient.id", patient.getId());
     
    //ADD NOTIFIER.. because a new Emergency with a call was created!
    //System.out.println("ADDING EMERGENCY REACHED LISTENER NOTIFIER");
     EmergencyService.getInstance().getMapEventsNotifier().addWorldEventNotifier(EventType.EMERGENCY_REACHED, new MapEmergencyReachedEventNotifier($emergency));
     

     // Insert the emergency in our world
     update($emergency);
     //Insert the patient in our world
     insert(patient);

     
     
       
end

rule "Fire Emergency without more information"
    ruleflow-group "select_ambulance"
    salience 10
    no-loop true
    when
        $processInstance: WorkflowProcessInstance()
        $emergency: Emergency($type : type == EmergencyType.FIRE)
    then
    	// Get the first ambulance that we have for this type of emergency
        Ambulance ambulance = (Ambulance)((List) ambulances.get($type)).get(0);
        // Get the first doctor that we have with the speciality related with the emergency
        Medic medic = (Medic)((List)doctors.get(MedicSpeciality.BURNS)).get(0);
        ambulance.setMedicOnBoard(medic);
        $emergency.setAmbulance(ambulance);
        //Update Emergency Information
        update($emergency);
		// Set the doctor.id and the ambulance.id as process variables for future references
        $processInstance.setVariable("doctor.id", medic.getId());
        $processInstance.setVariable("ambulance.id", ambulance.getId());
        $processInstance.setVariable("ambulance.description", ambulance.getDescription());
        
end

rule "Car Crash Emergency without more information"
    ruleflow-group "select_ambulance"
    salience 10
    no-loop true
    when
        $processInstance: WorkflowProcessInstance()
        $emergency: Emergency($type: type == EmergencyType.CAR_CRASH)
    then
    	// Get the first ambulance that we have for this type of emergency
        Ambulance ambulance = (Ambulance)((List) ambulances.get($type)).get(0);
        // Get the first doctor that we have with the speciality related with the emergency
        Medic medic = (Medic)((List)doctors.get(MedicSpeciality.BONES)).get(0);
        ambulance.setMedicOnBoard(medic);
        $emergency.setAmbulance(ambulance);
        //Update Emergency Information
        update($emergency);
		// Set the doctor.id and the ambulance.id as process variables for future references
        $processInstance.setVariable("doctor.id", medic.getId());
        $processInstance.setVariable("ambulance.id", ambulance.getId());
        $processInstance.setVariable("ambulance.description", ambulance.getDescription());
        
end

rule "Heart Attack Emergency without more information"
    ruleflow-group "select_ambulance"
    salience 10
    no-loop true
    when
        $processInstance: WorkflowProcessInstance()
        $emergency: Emergency($type: type == EmergencyType.HEART_ATTACK)
    then
    	// Get the first ambulance that we have for this type of emergency
        Ambulance ambulance = (Ambulance)((List) ambulances.get($type)).get(0);
        // Get the first doctor that we have with the speciality related with the emergency
        Medic medic = (Medic)((List)doctors.get(MedicSpeciality.REANIMATION)).get(0);
        ambulance.setMedicOnBoard(medic);
        $emergency.setAmbulance(ambulance);
        //Update Emergency Information
        update($emergency);
		// Set the doctor.id and the ambulance.id as process variables for future references
        $processInstance.setVariable("doctor.id", medic.getId());
        $processInstance.setVariable("ambulance.id", ambulance.getId());
        $processInstance.setVariable("ambulance.description", ambulance.getDescription());
        
end

rule "Car Crash Emergency with Patient older than 80 years old"
    ruleflow-group "select_ambulance"
    salience 5
    no-loop true
    when
        $processInstance: WorkflowProcessInstance()
        $emergency: Emergency($type: type == EmergencyType.CAR_CRASH)
        $patient: Patient(age > 80)
    then
    	// For this special situation we need to put in the ambulance a special stroller
        Ambulance ambulance = $emergency.getAmbulance();
        ambulance.addKit(new MedicalKit("special stroller", MedicSpeciality.BONES));
        
end